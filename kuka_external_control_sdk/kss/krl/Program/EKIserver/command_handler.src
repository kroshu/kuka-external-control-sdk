&ACCESS RVP
&PARAM DISKPATH = KRC:\R1\Program\ROS
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
DEF command_handler()
    ; Software License Agreement (BSD License)
    ;
    ; Copyright (c) 2022, Kuka Robotics Corp
    ; All rights reserved.
    ;
    ; Redistribution and use in source and binary forms, with or without
    ; modification, are permitted provided that the following conditions are met:
    ;
    ;      * Redistributions of source code must retain the above copyright
    ;        notice, this list of conditions and the following disclaimer.
    ;      * Redistributions in binary form must reproduce the above copyright
    ;        notice, this list of conditions and the following disclaimer in the
    ;        documentation and/or other materials provided with the distribution.
    ;      * Neither the name of the copyright holder, nor the names of its
    ;        contributors may be used to endorse or promote products derived
    ;        from this software without specific prior written permission.
    ;
    ; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
    ; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
    ; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
    ; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
    ; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
    ; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
    ; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
    ; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
    ; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
    ; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    ; POSSIBILITY OF SUCH DAMAGE.
END

GLOBAL DEF HandleCommand(cmd: IN)
    DECL Command cmd
    DECL CMDType cmd_enum
    DECL ControlMode ctrl_mode

    cmd_enum = ParseCommand(cmd.cmd_str[])

    IF cmd_enum == #CMD_NONE THEN
        ; If nothing was received, just RETURN
        RETURN
    ENDIF

    IF IsCommandValid(cmd_enum) THEN
        SWITCH cmd_enum
            CASE #CMD_INVALID
                SetLastEvent(#INVALID)
            CASE #CMD_CON
                SetLastEvent(#CONNECTED)
            CASE #CMD_START
                IF ROS_StartProgram() THEN
                    current_driver_state = #Active
                    SetLastEvent(#STARTED)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_CANCEL
                IF CancelProgram() THEN
                    current_driver_state = #Initialized
                    ROS_SelectControlMode(#INACTIVE)
                    SetLastEvent(#CANCELLED)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_RESET
                IF ResetProgram() THEN
                    current_driver_state = #INACTIVE
                    SetLastEvent(#RESET_OK)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_CHANGE
                ctrl_mode = ROS_IntToControlMode(cmd.control_mode)
                IF ctrl_mode == #INACTIVE THEN
                    SetLastEvent(#ERROR)
                    RETURN
                ENDIF
                ROS_SelectControlMode(ctrl_mode)
                current_driver_state = #INACTIVE
                SetLastEvent(#SWITCH_OK)
            CASE #CMD_DRIVES_ON
                $DRIVES_ENABLE = TRUE
                SetLastEvent(#DRIVES_TURNED_ON)
            CASE #CMD_DRIVES_OFF
                $DRIVES_ENABLE = FALSE
                SetLastEvent(#DRIVES_TURNED_OFF)
            CASE #CMD_CHANGE_CYCLE_TIME
                IF NOT ROS_SelectCycleTime(cmd.cycle_time) THEN
                    SetLastEvent(#ERROR)
                    RETURN
                ENDIF
                SetLastEvent(#CYCLE_TIME_CHANGED)
        ENDSWITCH
    ELSE
        SetLastEvent(#INVALID)
        MsgNotify("Invalid transition requested")
    ENDIF
END

DEFFCT CMDType ParseCommand(cmd[]: IN)
    CHAR cmd[]
    INT idx

    FOR idx = 1 TO CMD_COUNT
        IF (StrComp(cmd[], STRING_COMMAND_PAIRS[idx].cmd_string[], #NOT_CASE_SENS)) THEN
            RETURN STRING_COMMAND_PAIRS[idx].cmd_type
        ENDIF
    ENDFOR

    RETURN #CMD_INVALID
ENDFCT

DEFFCT BOOL IsCommandValid(cmd: IN)
    DECL CMDType cmd
    CHAR cmd_string[32]
    CHAR state_string[32]

    ; Info message
    cmd_string[] = CommandToString(cmd)
    state_string[] = DriverStateToString(current_driver_state)
    MsgNotifyTextPar("Got command %1 while in state %2", "CommandHandler", 0, cmd_string[], state_string[])

    ; Actual check
    SWITCH current_driver_state
        CASE #INITIALIZED
            RETURN IsCommandValidInInit(cmd)
        CASE #INACTIVE
            RETURN IsCommandValidInInactive(cmd)
        CASE #ACTIVE
            RETURN IsCommandValidInActive(cmd)
    ENDSWITCH
ENDFCT

GLOBAL DEF SetCurrentDriverState(state: IN)
    DECL DriverState state
    current_driver_state = state
END

; Maps the command enum to its string representation
DEFFCT CHAR [32] CommandToString(command: IN)
    DECL CMDType command
    DECL StringCommandPair pair
    INT idx

    FOR idx = 1 TO CMD_COUNT
        pair = STRING_COMMAND_PAIRS[idx]
        IF (command == pair.cmd_type) THEN
            RETURN pair.cmd_string[]
        ENDIF
    ENDFOR

    ; Unreachable
    RETURN "INVALID"
ENDFCT

; Maps the driver state to its string representation
DEFFCT CHAR [32] DriverStateToString(driver_state: IN)
    DECL DriverState driver_state
    INT idx

    FOR idx = 1 TO STATE_COUNT
        IF driver_state == STRING_STATE_PAIRS[idx].state THEN
            RETURN STRING_STATE_PAIRS[idx].state_string[]
        ENDIF
    ENDFOR

    ; Unreachable
    RETURN "INVALID"
ENDFCT

; Checks if the specified command enum is a valid command in the initialized state
DEFFCT BOOL IsCommandValidInInit(cmd: IN)
    DECL CMDType cmd
    INT idx

    FOR idx = 1 TO INITIALIZED_VALID_COUNT
        IF INIT_VALID_COMMANDS[idx] == cmd THEN
            RETURN TRUE
        ENDIF
    ENDFOR

    RETURN FALSE
ENDFCT

; Checks if the specified command enum is a valid command in the inactive state
DEFFCT BOOL IsCommandValidInInactive(cmd: IN)
    DECL CMDType cmd
    INT idx

    FOR idx = 1 TO INACTIVE_VALID_COUNT
        IF INACTIVE_VALID_COMMANDS[idx] == cmd THEN
            RETURN TRUE
        ENDIF
    ENDFOR

    RETURN FALSE
ENDFCT

; Checks if the specified command enum is a valid command in the active state
DEFFCT BOOL IsCommandValidInActive(cmd: IN)
    DECL CMDType cmd
    INT idx

    FOR idx = 1 TO ACTIVE_VALID_COUNT
        IF ACTIVE_VALID_COMMANDS[idx] == cmd THEN
            RETURN TRUE
        ENDIF
    ENDFOR

    RETURN FALSE
ENDFCT

; Resets the command handler state. Should be called whenever the external client disconnects.
GLOBAL DEF ROS_ResetCommandHandler()
    current_driver_state = #INITIALIZED
END
