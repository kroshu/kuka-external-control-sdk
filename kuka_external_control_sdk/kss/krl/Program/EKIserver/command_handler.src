&ACCESS RVP
&PARAM DISKPATH = KRC:\R1\Program\ROS
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
DEF command_handler()
    ; Software License Agreement (BSD License)
    ;
    ; Copyright (c) 2022, Kuka Robotics Corp
    ; All rights reserved.
    ;
    ; Redistribution and use in source and binary forms, with or without
    ; modification, are permitted provided that the following conditions are met:
    ;
    ;      * Redistributions of source code must retain the above copyright
    ;        notice, this list of conditions and the following disclaimer.
    ;      * Redistributions in binary form must reproduce the above copyright
    ;        notice, this list of conditions and the following disclaimer in the
    ;        documentation and/or other materials provided with the distribution.
    ;      * Neither the name of the copyright holder, nor the names of its
    ;        contributors may be used to endorse or promote products derived
    ;        from this software without specific prior written permission.
    ;
    ; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
    ; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
    ; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
    ; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
    ; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
    ; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
    ; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
    ; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
    ; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
    ; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    ; POSSIBILITY OF SUCH DAMAGE.
END

GLOBAL DEF HandleCommand(cmd: IN)
    DECL Command cmd
    DECL CMDType cmd_enum
    DECL BOOL cmd_success
    DECL ControlMode ctrl_mode

    cmd_enum = ParseCommand(cmd.cmd_str[])

    IF (cmd_enum == #CMD_NONE) THEN
        ; If nothing was received, just RETURN
        RETURN
    ENDIF

    IF (ValidateCommand(cmd_enum)) THEN
        SWITCH cmd_enum
            CASE #CMD_INVALID
                SetLastEvent(#INVALID)
            CASE #CMD_CON
                SetLastEvent(#CONNECTED)
            CASE #CMD_START
                cmd_success = StartProgram(GetProgName(GetControlMode()))
                IF (cmd_success) THEN
                    current_driver_state = #Active
                    SetLastEvent(#STARTED)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_CANCEL
                cmd_success = CancelProgram()
                IF cmd_success THEN
                    current_driver_state = #Initialized
                    SetControlMode(#INACTIVE)
                    SetLastEvent(#CANCELLED)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_RESET
                cmd_success = ResetProgram()
                IF cmd_success THEN
                    current_driver_state = #INACTIVE
                    SetLastEvent(#RESET_OK)
                ELSE
                    SetLastEvent(#ERROR)
                ENDIF
            CASE #CMD_CHANGE
                SWITCH cmd.control_mode
                    CASE 1
                        ctrl_mode = #JOINT_POSITION
                    CASE 5
                        ctrl_mode = #CART_POSITION
                    DEFAULT
                        SetLastEvent(#ERROR)
                        RETURN
                ENDSWITCH
                SetControlMode(ctrl_mode)
                current_driver_state = #INACTIVE
                SetLastEvent(#SWITCH_OK)
        ENDSWITCH
    ELSE
        SetLastEvent(#INVALID)
        MsgNotify("Invalid transition requested")
    ENDIF
END

DEFFCT CMDType ParseCommand(cmd[]: IN)
    CHAR cmd[]

    IF (StrComp(cmd[], "CON", #NOT_CASE_SENS)) THEN
        RETURN #CMD_CON
    ENDIF

    IF (StrComp(cmd[], "START", #NOT_CASE_SENS)) THEN
        RETURN #CMD_START
    ENDIF

    IF (StrComp(cmd[], "RESET", #NOT_CASE_SENS)) THEN
        RETURN #CMD_RESET
    ENDIF

    IF (StrComp(cmd[], "CANCEL", #NOT_CASE_SENS)) THEN
        RETURN #CMD_CANCEL
    ENDIF

    IF (StrComp(cmd[], "CHANGE", #NOT_CASE_SENS)) THEN
        RETURN #CMD_CHANGE
    ENDIF

    IF (StrComp(cmd[], "NONE", #NOT_CASE_SENS)) THEN
        RETURN #CMD_NONE
    ENDIF

    RETURN #CMD_INVALID
ENDFCT

DEFFCT BOOL ValidateCommand(cmd: IN)
    DECL CMDType cmd
    CHAR cmd_string[32]
    CHAR state_string[16]

    ; Info message
    cmd_string[] = CommandToString(cmd)
    state_string[] = DriverStateToString(current_driver_state)
    MsgNotifyTextPar("Got command %1 while in state %2", "CommandHandler", 0, cmd_string[], state_string[])

    ; Actual check
    IF (current_driver_state == #INITIALIZED) THEN
        RETURN (cmd == #CMD_CON) OR (cmd == #CMD_CHANGE)
    ENDIF

    IF (current_driver_state == #INACTIVE) THEN
        RETURN (cmd == #CMD_START) OR (cmd == #CMD_CHANGE) OR (cmd == #CMD_CANCEL)
    ENDIF

    ; If the driver is neither INITIALIZED nor INACTIVE, it must be ACTIVE
    RETURN (cmd == #CMD_CANCEL) OR (cmd == #CMD_RESET)
ENDFCT

DEFFCT CHAR [32] CommandToString(command: IN)
    DECL CMDType command
    SWITCH command
        CASE #CMD_NONE
            RETURN "NONE"
        CASE #CMD_CON
            RETURN "CONNECT"
        CASE #CMD_START
            RETURN "START"
        CASE #CMD_RESET
            RETURN "RESET"
        CASE #CMD_CANCEL
            RETURN "CANCEL"
        CASE #CMD_CHANGE
            RETURN "CHANGE"
        CASE #CMD_INVALID
            RETURN "INVALID"
    ENDSWITCH
ENDFCT

DEFFCT CHAR [16] DriverStateToString(driver_state: IN)
    DECL DriverState driver_state
    SWITCH driver_state
        CASE #INITIALIZED
            RETURN "INITIALIZED"
        CASE #INACTIVE
            RETURN "INACTIVE"
        CASE #ACTIVE
            RETURN "ACTIVE"
    ENDSWITCH
ENDFCT

GLOBAL DEF SetCurrentDriverState(state: IN)
    DECL DriverState state
    current_driver_state = state
END
