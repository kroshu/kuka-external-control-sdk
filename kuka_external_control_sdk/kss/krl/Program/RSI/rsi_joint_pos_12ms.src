&ACCESS RVP
&PARAM DISKPATH = KRC:\R1\Program\ROS
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
DEF rsi_joint_pos_12ms()
    ; Declaration of KRL variables
    DECL INT ret
    DECL INT cont_id

    ; FOLD INI
    ; FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ()
    INTERRUPT ON 3
    BAS (#INITMOV, 0)
    ; ENDFOLD (BASISTECH INI)
    ; ENDFOLD (INI)

    WAIT SEC 1.0

    ; Move to start position
    PTP $AXIS_ACT_MEAS

    ; Create RSI Context
    ret = RSI_CREATE("rsi_joint_pos", cont_id, TRUE)
    IF (ret <> RSIOK) THEN
        HALT
    ENDIF

    ; Set AxisCorr parameters to allow full range of motion
    ROS_SetAxisCorrLimits(cont_id)

    ; Start RSI execution
    ret = RSI_ON(#ABSOLUTE, #IPO)
    IF (ret <> RSIOK) THEN
        HALT
    ENDIF

    ; Sensor guided movement
    RSI_MOVECORR()

    ; Turn off RSI
    ret = RSI_OFF()
    IF (ret <> RSIOK) THEN
        HALT
    ENDIF

    PTP $AXIS_ACT_MEAS

    ret = RSI_DELETE(cont_id)
    IF (ret <> RSIOK) THEN
        HALT
    ENDIF
END
